plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.smartfeeder'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

configurations {
    koraBom
    annotationProcessor.extendsFrom(koraBom)
    compileOnly.extendsFrom(koraBom)
    implementation.extendsFrom(koraBom)
    testImplementation.extendsFrom(koraBom)
    testAnnotationProcessor.extendsFrom(koraBom)
}

dependencies {
    koraBom platform('ru.tinkoff.kora:kora-parent:1.2.9')

    annotationProcessor 'ru.tinkoff.kora:annotation-processors'
    testAnnotationProcessor 'ru.tinkoff.kora:annotation-processors'

    implementation 'ru.tinkoff.kora:config-hocon'
    implementation 'ru.tinkoff.kora:logging-logback'
    implementation 'ru.tinkoff.kora:json-module'
    implementation 'ru.tinkoff.kora:validation-module'
    implementation 'ru.tinkoff.kora:http-server-undertow'
    implementation 'ru.tinkoff.kora:database-jdbc'
    implementation 'ru.tinkoff.kora:openapi-management'

    implementation 'org.postgresql:postgresql:42.7.7'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.flywaydb:flyway-core:10.20.1'
    implementation 'org.flywaydb:flyway-database-postgresql:10.20.1'
    implementation 'org.mindrot:jbcrypt:0.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.2'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.assertj:assertj-core:3.26.3'
}

application {
    applicationName = 'smart-feeder-backend'
    mainClass = 'com.smartfeeder.Application'
    applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8']
}

tasks.test {
    useJUnitPlatform()
}

tasks.shadowJar {
    archiveBaseName.set('smart-feeder-backend')
    archiveClassifier.set('all')
    archiveVersion.set('')
}
